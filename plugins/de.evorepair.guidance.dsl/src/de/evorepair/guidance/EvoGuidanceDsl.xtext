grammar de.evorepair.guidance.EvoGuidanceDsl with de.evorepair.feature.mapping.repair.dsl.EvoMappingRepairDsl

import "http://darwinspl.de/evorepair/guidance/catalog/0.2" as dw_guidance
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://hyvar-project.eu/feature/1.0" as dw_feature
import "http://hyvar-project.eu/feature/expression/1.0" as dw_expression
import "http://darwinspl.de/evorepair/logic/0.5" as evo_logic
import "http://darwinspl.de/evorepair/variable/0.3" as evo_variables
import "http://darwinspl.de/evorepair/evolutionoperation/0.2" as evo_operations

EvoGuidanceContainer returns dw_guidance::EvoGuidanceContainer:
	tables+=EvoGuidanceTable (tables+=EvoGuidanceTable)*;
	
	
EvoGuidanceTable returns dw_guidance::EvoGuidanceTable: 
	'guidance_table' '{'
		'trigger'  '{'
			triggeringOperations+=[evo_operations::EvoOperation] (triggeringOperations+=[evo_operations::EvoOperation])*
		'}'
		
		'anomalies' '{'
			anomalies+=EvoAnomaly (anomalies+=EvoAnomaly)*
		'}'
	'}';


EvoAnomaly returns dw_guidance::EvoAnomaly:
	EvoMappingAnomaly | EvoConfigurationAnomaly;
	
	
EvoMappingAnomaly returns dw_guidance::EvoMappingAnomaly:
 	'mapping' id=ID '{'
		('type' ':' type=EvoAnomalyType ';')?
		'condition' ':' condition=EvoConfigurationFormula ';'
		'guidances' '{' 
			guidance+=EvoMappingGuidanceElement (guidance+=EvoMappingGuidanceElement)*
		'}' 
	'}';


EvoConfigurationAnomaly returns dw_guidance::EvoConfigurationAnomaly:
 	'configuration' id=ID '{'
		('type' ':' type=EvoAnomalyType ';')?
		'condition' ':' condition=EvoConfigurationFormula ';'
		'guidances' '{' 
			guidance+=EvoConfigurationGuidanceElement (guidance+=EvoConfigurationGuidanceElement)*
		'}' 
	'}';
		
	
EvoMappingGuidanceElement returns dw_guidance::EvoGuidanceElement:
	'guidance' '{'
		'type' ':' type=EvoGuidanceType ';'
		'description' ':' description=STRING ';'
		('action' ':' action=EvoMappingFormula ';')?
	'}';


EvoConfigurationGuidanceElement returns dw_guidance::EvoGuidanceElement:
	'guidance' '{'
		'type' ':' type=EvoGuidanceType ';'
		'description' ':' description=STRING ';'
		('action' ':' action=EvoConfigurationFormula ';')?
	'}';


EvoMappingFormula returns dw_guidance::EvoMappingFormula:
	term=EvoMappingRepairDsl::EvoMappingRepairExpression
;


EvoConfigurationFormula returns dw_guidance::EvoConfigurationFormula:
	term=EvoLogicDsl::EvoExpression
;


enum EvoAnomalyType returns dw_guidance::EvoAnomalyType:
	NOT_SPECIFIED = 'not_specified' | DEFECT = 'defect' | BEHAVIORAL_CHANGE = 'behavioral_change';


enum EvoGuidanceType returns dw_guidance::EvoGuidanceType:
				AUTOMATIC_DEFAULT = 'automatic_repair' | INTERACTIVE_DEFAULT = 'interactive_repair' | WARNING = 'warning';