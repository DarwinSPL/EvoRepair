grammar de.evorepair.evolution.variable.EvoVariableDsl with org.eclipse.xtext.common.Terminals

generate evoVariableDsl "http://www.evorepair.de/evolution/variable/EvoVariableDsl"

import "http://darwinspl.de/evorepair/variable/0.2"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://hyvar-project.eu/feature/1.0" as feature

Model:
	'import' importURI=QualifiedName
	variables+=EvoVariable*;

EvoVariable:
	EvoConfigurationVariable | EvoFeatureVariable | EvoGroupVariable | EvoSetVariable | EvoMappingVariable;


EvoFeatureVariable: 
	'feature' name=ID (relation=EvoFeatureRelation)? ('value' '=' feature=[feature::HyFeature | QualifiedName])?;

EvoFeatureRelation:
	featureType=EvoFeatureVariableType '(' relatedFeatures+=[EvoFeatureVariable] (',' relatedFeatures+=[EvoFeatureVariable])? ')';

EvoGroupVariable:
	'group' name=ID ('value' '=' group=[feature::HyGroup | QualifiedName])?;

EvoSetVariable: 
	'set' name=ID ('(' elements+=[EvoVariable] (',' elements+=[EvoVariable]) ')')?;

EvoMappingVariable:
	'mapping' name=ID;

EvoConfigurationVariable:
	'configuration' name=ID;

enum EvoFeatureVariableType:
	EvoParent = 'parentOf' |
	EvoSibling = 'siblingOf' |
	EvoChild = 'childOf';

QualifiedName returns ecore::EString:
	ID ("." ID)*;
