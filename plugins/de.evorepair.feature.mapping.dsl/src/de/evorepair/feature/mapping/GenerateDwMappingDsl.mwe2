module de.evorepair.feature.mapping.GenerateDwMappingDsl

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {

	bean = org.eclipse.emf.mwe.utils.StandaloneSetup{
		scanClassPath=true
		
        uriMap = {
            from = "platform:/plugin/org.eclipse.emf.ecore/model/Ecore.genmodel"
            to = "platform:/resource/org.eclipse.emf.ecore/model/Ecore.genmodel"
        }

        uriMap = {
            from = "platform:/plugin/eu.hyvar.feature.data/model/DataValues.genmodel"
            to = "platform:/resource/eu.hyvar.feature.data/model/DataValues.genmodel"
        }                    
        
        uriMap = {
        	from = "platform:/plugin/org.deltaecore.core.decore/model/DEcore.genmodel"
        	to = "platform:/resource/org.deltaecore.core.decore/model/DEcore.genmodel"
        }   
        
        uriMap = {
            from = "platform:/plugin/eu.hyvar.evolution/model/Evolution.genmodel"
            to = "platform:/resource/eu.hyvar.evolution/model/Evolution.genmodel"
        }     

       
         // register the ecore model file directly otherwise DeltaEcore couldn't be referenced
		registerEcoreFile ="platform:/resource/org.eclipse.emf.ecore/model/Ecore.ecore"         
  
        registerGenModelFile = "platform:/resource/org.deltaecore.core.decore/model/DEcore.genmodel"
        registerGenModelFile = "platform:/resource/eu.hyvar.feature.data/model/DataValues.genmodel"
        registerGenModelFile = "platform:/resource/eu.hyvar.feature.mapping/model/Mapping.genmodel"
        
        registerGeneratedEPackage = "org.eclipse.emf.ecore.EcorePackage"
        registerGeneratedEPackage = "org.deltaecore.core.decore.DEcorePackage"
        registerGeneratedEPackage = "eu.hyvar.dataValues.HyDataValuesPackage"
        
		registerGeneratedEPackage = "de.evorepair.logic.evologic.EvoLogicPackage"
        registerGeneratedEPackage = "eu.hyvar.feature.mapping.MappingPackage"     
        registerGeneratedEPackage = "eu.hyvar.feature.expression.HyExpressionPackage"  
   	}	
	
	component = XtextGenerator {		
		configuration = {
			project = StandardProjectConfig {
				baseName = "de.evorepair.feature.mapping.dsl"
				rootPath = rootPath
				runtimeTest = {
					enabled = false
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = false
				}
				createEclipseMetaData = true
				
				
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\r\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
				
				preferXtendStubs = false
			}
		}
		language = StandardLanguage {
			name = "de.evorepair.feature.mapping.DwMappingDsl"
			fileExtensions = "hymapping,hymappingmodel"
			
			referencedResource = "platform:/resource/eu.hyvar.feature.mapping/model/Mapping.genmodel"
			referencedResource = "platform:/resource/de.evorepair.feature.expression.dsl/model/generated/EvoExpressionDsl.genmodel"
			
			serializer = {
				generateStub = false
			}
			
			scopeProvider = {
				generateStub = false
			}
			
			newProjectWizardForEclipse = {
				generate = true
			}
			
			validator = {
				generateStub = false
			}
		}
	}
}
