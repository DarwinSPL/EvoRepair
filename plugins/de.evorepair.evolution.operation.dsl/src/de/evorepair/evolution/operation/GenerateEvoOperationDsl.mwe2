module de.evorepair.evolution.operation.GenerateEvoOperationDsl

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {

	bean = org.eclipse.emf.mwe.utils.StandaloneSetup{
		scanClassPath = true
		
		registerGeneratedEPackage = "de.evorepair.evolution.variable.EvoVariablePackage"
	}
	
	component = XtextGenerator {		
		configuration = {
			project = StandardProjectConfig {
				baseName = "de.evorepair.evolution.operation.dsl"
				rootPath = rootPath

				eclipsePlugin = {
					enabled = true
				}

				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\r\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		
		
		language = StandardLanguage {
			name = "de.evorepair.evolution.operation.EvoOperationDsl"
			fileExtensions = "evooperation"
			
			referencedResource = "platform:/resource/de.evorepair.evolution.operation/model/EvoOperation.genmodel"
						
			serializer = {
				generateStub = false
			}
			
			validator = {
				generateStub = false
			}
			
			emfGenerator = {
				skipGenerate = true				
			}
		}
	}
}
