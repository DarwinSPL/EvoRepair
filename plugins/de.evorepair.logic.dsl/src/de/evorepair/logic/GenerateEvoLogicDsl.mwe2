module de.evorepair.logic.GenerateEvoDsl

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

var projectName = "de.evorepair.logic.dsl"
var runtimeProject = "../${projectName}"

Workflow {
	bean = org.eclipse.emf.mwe.utils.StandaloneSetup{
		scanClassPath = true
		
        uriMap = {
            from = "platform:/plugin/de.evorepair.evolution.operation/model/EvoOperation.genmodel"
            to = "platform:/resource/de.evorepair.evolution.operation/model/EvoOperation.genmodel"
        }

        uriMap = {
            from = "platform:/plugin/de.evorepair.evolution.variable/model/EvoVariable.genmodel"
            to = "platform:/resource/de.evorepair.evolution.variable/model/EvoVariable.genmodel"
        }
        
		platformUri = "${runtimeProject}/.."
		
		registerGeneratedEPackage = "de.evorepair.evolution.evooperation.EvoOperationPackage"
		registerGeneratedEPackage = "de.evorepair.evolution.evovariable.EvoVariablePackage"
		registerGenModelFile = "platform:/resource/de.evorepair.evolution.operation/model/EvoOperation.genmodel"
		registerGenModelFile = "platform:/resource/de.evorepair.evolution.variable/model/EvoVariable.genmodel"
		
	}
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "de.evorepair.logic.dsl"
				rootPath = rootPath
				runtimeTest = {
					//enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					//enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\r\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		language = StandardLanguage {
			name = "de.evorepair.logic.EvoDsl"
			fileExtensions = "evodsl"
			
			referencedResource = "platform:/resource/de.evorepair.evolution.operation/model/EvoOperation.genmodel"
			referencedResource = "platform:/resource/de.evorepair.evolution.variable/model/EvoVariable.genmodel"
			referencedResource = "platform:/resource/de.evorepair.logic.firstorder/model/EvoFirstOrder.genmodel"
			
			fragment = ecore2xtext.Ecore2XtextValueConverterServiceFragment2 auto-inject {}

			formatter = {
				generateStub = true
			}

			serializer = {
				generateStub = true
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}
		}
	}
}
